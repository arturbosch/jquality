
plugins {
	id "com.jfrog.bintray" version "1.7.3"
	id 'com.github.ben-manes.versions' version '0.14.0'
//	id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'io.gitlab.arturbosch.smartsmells'
version '1.0.0.RC2'

repositories {
	mavenCentral()
	mavenLocal()
	maven { url "http://dl.bintray.com/arturbosch/code-analysis" }
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: uri('../repo'))
		}
	}
}

bintray {
	user = System.getenv("BINTRAY_USER") ?: ""
	key = System.getenv("BINTRAY_API_KEY") ?: ""
	publications = ["SmartSmellsPublication"]

	pkg {
		repo = 'code-analysis'
		name = 'SmartSmells'
		userOrg = 'arturbosch'
		licenses = ['Apache-2.0']
		vcsUrl = "https://github.com/arturbosch/SmartSmells"

		version {
			name = project.version
			released = new Date()
		}
	}
}

publishing {
	publications {
		SmartSmellsPublication(MavenPublication) {
			from components.java
			groupId project.group
			artifactId project.name
			version project.version
		}
	}
}

task fatJar(type: Jar) {
	manifest {
		attributes 'Implementation-Title': 'SmartSmells',
				'Implementation-Version': version,
				'Main-Class': 'io.gitlab.arturbosch.smartsmells.Main'
	}
	baseName = project.name
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

test {
	exclude '**/system/**'
	testLogging {
		exceptionFormat = 'full'
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task systemTest( type: Test ) {
	exclude '/io/gitlab/arturbosch/**'
}

task unitTest( type: Test ) {
	exclude '**/system/**'
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.9'
	compile 'com.github.javaparser:javaparser-core:3.1.2'
	compile 'io.gitlab.arturbosch.jpal:jpal:1.0.0.RC4.b4'
	compile 'org.yaml:snakeyaml:1.18'
	compile 'com.beust:jcommander:1.64'
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'
}
