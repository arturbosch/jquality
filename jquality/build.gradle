plugins {
	id "org.sonarqube" version "2.7"
	id "com.github.johnrengelman.shadow" version "7.1.0"
	id 'com.github.ben-manes.versions' version '0.39.0'
}

apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'application'

mainClassName = 'io.gitlab.arturbosch.smartsmells.Main'

repositories {
	mavenCentral()
	mavenLocal()
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

publishing {
	publications {
		jqualityPublication(MavenPublication) {
			artifact sourcesJar
			from components.java
			groupId project.group
			artifactId project.name
			version project.version
		}
	}
}

test {
	exclude '**/system/**'
	testLogging {
		exceptionFormat = 'full'
	}
}

task systemTest(type: Test) {
	exclude '/io/gitlab/arturbosch/**'
}

task unitTest(type: Test) {
	exclude '**/system/**'
}

dependencies {
	api "io.gitlab.arturbosch.jpal:jpal:$jpalVersion"
	implementation "org.yaml:snakeyaml:$yamlVersion"
	implementation "com.beust:jcommander:$jcommanderVersion"
	testImplementation "org.spockframework:spock-core:$spockVersion"
}
